apply plugin: 'java'
apply plugin: 'maven-publish'

sourceCompatibility = 1.8
group 'com.mineinabyss.spigot'
version '0.1.0-alpha'

if (project.hasProperty("buildNo")) version += ".$buildNo"


repositories {
    mavenCentral()
    jcenter()
    maven {
        name = 'spigotmc-repo'
        url = 'https://hub.spigotmc.org/nexus/content/groups/public/'
    }
}


publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/MineInAbyss/DangerousWaters")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}

dependencies {
    implementation 'org.spigotmc:spigot-api:1.15-R0.1-SNAPSHOT'
}

//Move into plugins folder
if (project.hasProperty("plugin_path") && plugin_path) {
    println("Copying to plugin directory $plugin_path")
    task copyJar(type: Copy) {
        from jar // here it automatically reads jar file produced from jar task
        into plugin_path
    }
    build.dependsOn copyJar
}